0 REM FUJINET BOUNCE BY @KAYSAVETZ, DEC 27 2020
2 GOSUB 2000:TRAP 40000
10 GRAPHICS 3+16
20 COLOR 1:PLOT 0,0:DRAWTO 39,0:PLOT 0,23:DRAWTO 39,23
30 IF P=0 THEN PLOT 0,0:DRAWTO 0,23
40 IF P=1 THEN PLOT 39,0:DRAWTO 39,23
45 IF P=0 THEN X=10:Y=10:DX=1:DY=-1
46 IF P=1 THEN INPUT #1,Y,DX,DY:X=1
60 IF Y+DY>22 OR Y+DY=0 THEN DY=-DY
70 IF (P=1 AND X+DX>38) OR (P=0 AND X+DX=0) THEN DX=-DX
75 COLOR 0:PLOT X,Y
80 X=X+DX:Y=Y+DY
90 IF (P=0 AND X+DX>38) OR (P=1 AND X+DX<1) THEN SW=1
95 IF SW THEN ? #1;Y;",";DX;",";DY:COLOR 0:PLOT X,Y
96 IF SW THEN INPUT #1,Y,DX,DY
100 IF SW AND P=1 THEN X=1
110 IF SW AND P=0 THEN X=39
120 SW=0
160 COLOR 2:PLOT X,Y
190 FOR Q=1 TO 20:NEXT Q
200 GOTO 60
2000 REM GET HOSTNAME OR LISTEN
2001 DIM H$(64),D$(128)
2010 GRAPHICS 0:? "}":POKE 82,2
2020 ? "                BOUNCE   ":? 
2030 ? "ENTER HOSTNAME OR RETURN TO LISTEN"
2040 INPUT #16,H$
2050 IF H$="" THEN 2100
2060 D$="N:TCP://":D$(LEN(D$)+1)=H$:D$(LEN(D$)+1)=":6502/"
2061 TRAP 2090
2070 ? :? "CONNECTING TO: ";H$:OPEN #1,12,0,D$
2080 ? :? "CONNECTED!":P=1:REM AWAY PLAYER
2081 FOR I=255 TO 0 STEP -16:SOUND 0,I,12,12:FOR DE=0 TO 20:NEXT DE:NEXT I:SOUND 0,0,0,0:P=1:RETURN 
2090 ? :? "COULD NOT CONNECT."
2091 FOR I=0 TO 255 STEP 16:SOUND 0,I,12,12:FOR DE=0 TO 20:NEXT DE:NEXT I:SOUND 0,0,0,0:CLOSE #1:GOTO 2000
2100 REM LISTEN FOR CONNECTION
2101 P=0:REM HOME PLAYER
2110 ? :? "LISTENING..."
2120 D$="N:TCP://:6502/"
2130 OPEN #1,12,0,D$
2140 STATUS #1,A:IF PEEK(748)=0 THEN 2140
2150 ? :? "ACCEPTING..."
2160 XIO 65,#1,12,0,"N:"
2170 FOR I=255 TO 0 STEP -16:SOUND 0,I,12,12:FOR DE=0 TO 20:NEXT DE:NEXT I:SOUND 0,0,0,0
2180 P=0
2190 RETURN 
